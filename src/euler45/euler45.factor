! Triangle, pentagonal, and hexagonal numbers are
! generated by the following formulae:

! Triangle   T(n)=n(n+1)/2  1, 3, 6, 10, 15, ...
! Pentagonal P(n)=n(3n−1)/2 1, 5, 12, 22, 35, ...
! Hexagonal  H(n)=n(2n−1)   1, 6, 15, 28, 45, ...
! It can be verified that T(285) = P(165) = H(143) = 40755.

! Find the next triangle number that is also pentagonal
! and hexagonal.

USE: kernel
USE: math
USE: math.functions
USE: math.quadratic
IN: euler45

: triangle ( n -- x )
    dup 1 + * 2 / ;

: pentagonal? ( x -- ? )
    neg -0.5 1.5 quadratic drop dup round = ;

: hexagonal? ( x -- ? )
    neg -1 2 quadratic drop dup round = ;

: pentahexagonal? ( x -- ? )
    [ pentagonal? ]
    [ hexagonal? ]
    bi and ;

: euler45 ( -- )
    286
    [ dup triangle pentahexagonal? not ]
    [ 1 + ]
    while
    triangle . ;

MAIN: euler45
