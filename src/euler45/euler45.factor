! Triangle, pentagonal, and hexagonal numbers are
! generated by the following formulae:

! Triangle   T(n)=n(n+1)/2  1, 3, 6, 10, 15, ...
! Pentagonal P(n)=n(3n−1)/2 1, 5, 12, 22, 35, ...
! Hexagonal  H(n)=n(2n−1)   1, 6, 15, 28, 45, ...
! It can be verified that T(285) = P(165) = H(143) = 40755.

! Find the next triangle number that is also pentagonal
! and hexagonal.

USE: combinators.short-circuit
USE: kernel
USE: lists
USE: lists.lazy
USE: math
USE: math.functions
USE: prettyprint
IN: euler45

: triangle ( n -- x )
    dup 1 + * 2 / ;

: pentagonal? ( x -- ? )
    24 * 1 + sqrt 6 mod 5.0 = ;

: hexagonal? ( x -- ? )
    8 * 1 + sqrt 4 mod 3.0 = ;

: euler45 ( -- )
    286 lfrom
    [ triangle ] lmap-lazy
    [ pentagonal? ] lfilter
    [ hexagonal? ] lfilter car . ;

MAIN: euler45
